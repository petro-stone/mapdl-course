!! DADOS                                                       !!
! Exercício: 2.A
! Descrição: Análise estática linear de pórtico
! Unidades: m (metro), N (Newton), s (segundo)

!! PRÉ-PROCESSO                                                !!
/prep7

! Elemento BEAM188
ET,1,188
KEYOPT,1,3,2

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3

! KEYPOINTS
! Pórtico
K,1,0,0,0
K,2,0,3,0
K,3,4,3,0
K,4,4,0,0
! Auxiliares
K,5,-0.5,0,0
K,6,-0.5,3,0
K,7,0,3.5,0
K,8,4,3.5,0
K,9,4.5,3,0
K,10,4.5,0,0

! Lines
L,1,2
L,2,3
L,4,3

! PERFIS
! Perfil 1: W 150 x 22,5 (H)
SECTYPE,1,BEAM,I,W150X22,3
SECDATA,0.152,0.152,0.152,0.0066,0.0066,0.0058
! Perfil 2: W 310 x 21,0
SECTYPE,2,BEAM,I,W310X21,3
SECDATA,0.101,0.101,0.303,0.0057,0.0057,0.0051
! Perfil 3: W 200 x 52,0 (H)
SECTYPE,3,BEAM,I,H200X52,3
SECDATA,0.204,0.204,0.206,0.0126,0.0126,0.0079

! Definir atributos das linhas
! Pilar esquerdo
LSEL,S,LINE,,1
LATT,1,,,,5,6,1
LSEL,ALL
! Viga
LSEL,S,LINE,,2
LATT,1,,,,7,8,2
LSEL,ALL
! Pilar direito
LSEL,S,LINE,,3
LATT,1,,,,10,9,3
LSEL,ALL

! APOIOS
! Engaste
DK,4,UX,0,,,UY,UZ,ROTX,ROTY,ROTZ
! Apoio articulado
DK,1,UX,0,,,UY,UZ

! Criar malha
LESIZE,ALL,,,50
LMESH,ALL

! CARREGAMENTO
! Pilar esquerdo
LSEL,S,LINE,,1
ESLL,S
SFBEAM,ALL,1,PRES,-7000
ALLSEL
! Viga
LSEL,S,LINE,,2
ESLL,S
SFBEAM,ALL,1,PRES,18000
ALLSEL

! Ativar exibição de seções transversais
/eshape,1

! Exibir elementos
EPLOT

!! SOLUÇÃO                                                     !!
/solu
SOLVE

!! PÓS-PROCESSO                                                !!
/post1

! Ler resultados
SET,,1

! Plotar deformada
PLDISP

! Listar reações
PRRSOL

! Listar forças nas barras
PRESOL,F

! CRIAR TABELA
! Forças (i)
ETABLE,FXi,SMISC,1
ETABLE,FZi,SMISC,5
ETABLE,FYi,SMISC,6

! Momentos (i)
ETABLE,MYi,SMISC,2
ETABLE,MZi,SMISC,3
ETABLE,MTi,SMISC,4

! Forças (j)
ETABLE,FXj,SMISC,14
ETABLE,FZj,SMISC,18
ETABLE,FYj,SMISC,19

! Momento (j)
ETABLE,MYj,SMISC,15
ETABLE,MZj,SMISC,16
ETABLE,MTj,SMISC,17

! Tensão axial direta
ETABLE,AXi,SMISC,31
ETABLE,AXj,SMISC,36

! Tensão de flexão em Y+
ETABLE,BSposYi,SMISC,32
ETABLE,BSposYj,SMISC,37

! Tensão de flexão em Y-
ETABLE,BSnegYi,SMISC,33
ETABLE,BSnegYj,SMISC,38

! Tensão de flexão em Z+
ETABLE,BSposZi,SMISC,34
ETABLE,BSposZj,SMISC,39

! Tensão de flexão em Z-
ETABLE,BSnegZi,SMISC,35
ETABLE,BSnegZj,SMISC,40

! Tensão limite
SALLOW,345E6

! FATOR DE SEGURANÇA
! Tensão axial
SFCALC,SFAXi,AXi,,3
SFCALC,SFAXj,AXj,,3

! Tensão de flexão em Y+
SFCALC,SFBSPYi,BSposYi,,3
SFCALC,SFBSPYj,BSposYj,,3

! Tensão de flexão em Y-
SFCALC,SFBSNYi,BSnegYi,,3
SFCALC,SFBSNYj,BSnegYj,,3

! Tensão de flexão em Z+
SFCALC,SFBSPZi,BSposZi,,3
SFCALC,SFBSPZj,BSposZj,,3

! Tensão de flexão em Z-
SFCALC,SFBSNZi,BSnegZi,,3
SFCALC,SFBSNZj,BSnegZj,,3


! DIAGRAMAS
! FORÇAS
! Força axial
PLLS,FXi,FXj,1
! Força cortante FY
PLLS,FYi,FYj,1
! Força cortante FZ
PLLS,FZi,FZj,1

! MOMENTOS
! Momento MY
PLLS,MYi,MYj,1
! Momento MZ
PLLS,MZi,MZj,1
! Momento torçor
PLLS,MTi,MTj,1

! TENSÕES
! Tensão axial
PLLS,AXi,AXj
! Tensão de flexão em Y+
PLLS,BSposYi,BSposYj
! Tensão de flexão em Y-
PLLS,BSnegYi,BSnegYj
! Tensão de flexão em Z+
PLLS,BSposZi,BSposZj
! Tensão de flexão em Z-
PLLS,BSnegZi,BSnegZj

! FATORES DE SEGURANÇA
! Tensão axial
PLLS,SFAXi,SFAXj
! Tensão de flexão em Y+
PLLS,SFBSPYi,SFBSPYj
! Tensão de flexão em Y-
PLLS,SFBSNYi,SFBSNYj
! Tensão de flexão em Z+
PLLS,SFBSPZi,SFBSPZj
! Tensão de flexão em Z-
PLLS,SFBSNZi,SFBSNZj

! Tensão de Von Mises
PLNSOL,S,EQV

FINISH
