!! DADOS                                                       !!
! Exercício: 1.C
! Descrição: Análise estática linear de treliça plana com tirantes
! Unidades: m (metro), N (Newton), s (segundo)

!! PRÉ-PROCESSO                                                !!
/prep7

! Elemento LINK180
ET,1,180

! Real Constants
R,1,1.81E-3
R,2,0.12E-3,,1

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3

! Nós
N,1,0,0,0
N,2,0,2,0
N,3,0,4,0
NGEN,4,3,1,3,1,4

! ELEMENTOS
! Pórticos
REAL,1
E,1,2
E,2,3
E,4,5
E,5,6
E,7,8
E,8,9
E,10,11
E,11,12
E,2,5
E,5,8
E,8,11
E,3,6
E,6,9
E,9,12
! Contraventamento
REAL,2
E,7,11
E,10,8
E,8,12
E,11,9

! Apoios
D,1,UX,0,,,,UY
D,4,UX,0,,,,UY
D,7,UX,0,,,,UY
D,10,UX,0,,,,UY

! Carregamento
F,3,FX,17320
F,3,FY,-10000

! Ativar exibição de seções transversais
/eshape,1

! Exibir elementos
EPLOT

!! SOLUÇÃO                                                     !!
/solu
NLGEOM,ON
SOLVE

!! PÓS-PROCESSO                                                !!
/post1

! Ler resultados
SET,,1

! Plotar deformada
PLDISP

! Listar reações
PRRSOL

! Listar forças nas barras
PRESOL,F

! Criar tabela
ETABLE,TENSNORM,LS,1
ETABLE,DEFAXIAL,LEPEL,1
ETABLE,FORAXIAL,SMISC,1

! Plotar tensão normal
PLETAB,TENSNORM,NOAV

! Plotar deformação axial
PLETAB,DEFAXIAL,NOAV

! Plotar força axial
PLETAB,FORAXIAL,NOAV

! Listar tensões normais
PRETAB,TENSNORM

! Listar deformações axiais
PRETAB,DEFAXIAL

! Listar forças axiais
PRETAB,FORAXIAL

FINISH
