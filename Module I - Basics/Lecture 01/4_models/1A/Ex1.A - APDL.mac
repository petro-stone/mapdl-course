!! DADOS                                                       !!
! Exercício: 1.A
! Descrição: Análise estática linear de treliça plana
! Unidades: m (metro), N (Newton), s (segundo)

!! PRÉ-PROCESSO                                                !!
/prep7

! Elemento LINK180
ET,1,180

! Real Constants
R,1,1.26E-3

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3

! NÓS
! Linha superior
N,1,0,1
N,2,1,1
N,3,2,1
N,4,3,1
! Linha inferior
N,5,0,0
N,6,1,0
N,7,2,0

! ELEMENTOS
! Banzo superior
E,1,2
E,2,3
E,3,4
! Banzo inferior
E,5,6
E,6,7
! Montantes
E,5,1
E,6,2
E,7,3
! Diagonais
E,5,2
E,6,3
E,7,4

! APOIOS
! Superior
D,1,UX,0,,,,UY
! Inferior
D,5,UX,0

! Carregamento
F,4,FY,-10000

!! SOLUÇÃO                                                     !!
! Solucionar
/solu
SOLVE

!! PÓS-PROCESSO                                                !!
/post1

! Ler resultados
SET,,1

! Plotar deformada
PLDISP

! Listar reações
PRRSOL

! Listar forças nas barras
PRESOL,F

! Criar tabela
ETABLE,TENSNORM,LS,1
ETABLE,DEFAXIAL,LEPEL,1
ETABLE,FORAXIAL,SMISC,1

! Plotar tensão normal
PLETAB,TENSNORM,NOAV

! Plotar deformação axial
PLETAB,DEFAXIAL,NOAV

! Plotar força axial
PLETAB,FORAXIAL,NOAV

! Listar tensões normais
PRETAB,TENSNORM

! Listar deformações axiais
PRETAB,DEFAXIAL

! Listar forças axiais
PRETAB,FORAXIAL

! Criar animação
ANMODE,50,0.02

FINISH

