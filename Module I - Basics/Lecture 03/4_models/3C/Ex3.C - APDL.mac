!! DADOS                                                       !!
! Exercício: 3.C
! Descrição: Análise estática não-linear de placa apoiada em pilar
! Unidades: m (metro), N (Newton), s (segundo), kg (quilograma)

!! PRÉ-PROCESSO                                                !!
/prep7

! Elementos BEAM188 e SHELL181
ET,1,188
ET,2,181
KEYOPT,1,3,2

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3
MP,DENS,1,7850
TB,BISO,1,1,2
TBTEMP,0
TBDATA,,345E6

! SEÇÕES TRANSVERSAIS
! Barras
! Perfil 1: TUBO 120x5
SECTYPE,1,BEAM,CTUBE,TUB120X5
SECDATA,0.055,0.06,20
! Perfil 2: W 150 x 13,0
SECTYPE,2,BEAM,I,W150X13,2
SECDATA,0.1,0.1,0.148,0.0049,0.0049,0.0043
! Chapa
SECTYPE,3,SHELL,,PLACA
SECDATA,0.025,1,,5
SECOFFSET,USER,-(0.148+0.025)/2

! KEYPOINTS
! Extremidades
K,1,0,0,0
K,2,0,3,0
K,3,0,4,0
K,4,2,3,0
K,5,2,4,0
! Orientação
K,6,0,3,1
K,7,2,3,1
K,8,2,4,1

! Lines
L,1,2
L,2,3
L,2,4
L,4,5
L,5,3

! Areas
AL,3,4,5,2

! Apoio
DK,1,ALL

! Cargas
SFA,ALL,2,PRES,3000
/psf,PRES,NORM,2

! ATRIBUTOS DAS LINHAS
! Pilar
LSEL,S,LINE,,1,2,1
LATT,1,,1,,,,1
LSEL,ALL
! Barra inferior
LSEL,S,LINE,,3
LATT,1,,1,,6,,2
LSEL,ALL
! Barra superior
LSEL,S,LINE,,5
LATT,1,,1,,8,,2
LSEL,ALL
! Barra vertical
LSEL,S,LINE,,4
LATT,1,,1,,7,,2
LSEL,ALL

! ATRIBUTOS DAS ÁREAS
ASEL,S,AREA,,1
AATT,1,,2,,3
ALLSEL

! Criar malha
LESIZE,ALL,0.05
AESIZE,ALL,0.05
LMESH,ALL
AMESH,ALL

! Exibição
/eshape,1
EPLOT

!! SOLUÇÃO                                                     !!
/solu
NSUBST,100
OUTRES,ERASE
OUTRES,ALL,ALL
SOLVE

!! PÓS-PROCESSO                                                !!
/post1

! Ler resultados
SET,1,LAST

! Plotar deformada
PLDISP

! Listar reações
PRRSOL

! CRIAR TABELA - BARRAS
! Forças (i)
ETABLE,FXi,SMISC,1
ETABLE,FZi,SMISC,5
ETABLE,FYi,SMISC,6
! Momentos (i)
ETABLE,MYi,SMISC,2
ETABLE,MZi,SMISC,3
ETABLE,MTi,SMISC,4
! Forças (j)
ETABLE,FXj,SMISC,14
ETABLE,FZj,SMISC,18
ETABLE,FYj,SMISC,19
! Momento (j)
ETABLE,MYj,SMISC,15
ETABLE,MZj,SMISC,16
ETABLE,MTj,SMISC,17

! CRIAR TABELA - CHAPA
! Tensão de von Mises
ETABLE,TENEQV,S,EQV
! Deslocamento em Z
ETABLE,DESLOCZ,U,Z
! Momento X local
ETABLE,MOMXLOC,SMISC,4
! Momento Y local
ETABLE,MOMYLOC,SMISC,5
! Momento XY local
ETABLE,MOM12,SMISC,6

! DIAGRAMAS
! FORÇAS
! Força axial
PLLS,FXi,FXj,1
! Força cortante FY
PLLS,FYi,FYj,1
! Força cortante FZ
PLLS,FZi,FZj,1
! MOMENTOS
! Momento MY
PLLS,MYi,MYj,1
! Momento MZ
PLLS,MZi,MZj,1
! Momento torçor
PLLS,MTi,MTj,1
! Chapa
PLETAB,TENEQV,AVG
PLETAB,DESLOCZ,AVG
PLETAB,MOMXLOC,AVG
PLETAB,MOMYLOC,AVG
PLETAB,MOM12,AVG

! Tensão de Von Mises
/gline,,-1
PLNSOL,S,EQV

FINISH
