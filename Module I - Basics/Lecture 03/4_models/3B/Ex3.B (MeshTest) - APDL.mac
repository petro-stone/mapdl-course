!! DADOS                                                       !!
! Exercício: 3.B
! Descrição: Análise estática linear de laje retangular (estudo de malha)
! Unidades: m (metro), N (Newton), s (segundo), kg (quilograma)
! Versão: 1.1 (18/05/2014)

!! DADOS DE ENTRADA                                            !!
MinEsize=0.02
MaxEsize=1.0
EsizeStep=0.005
EspLaje=0.12
fck=25E6
Poisson=0.2
ConcDens=2500
L1=5.0
L2=3.5
Load=8000
Gravity=9.81

!! DADOS CALCULADOS                                            !!
ElasticModulus=4760*SQRT(fck*1E-6)*1E6
ModelCount=NINT((MaxEsize-MinEsize)/EsizeStep)+1

!! DEFINIÇÕES PRÉVIAS                                          !!
!! PRÉ-PROCESSO                                                !!
/prep7

! Elemento SHELL181
ET,1,181

! Seção transversal
SECTYPE,1,SHELL,,LAJE,3
SECDATA,EspLaje,1

! Propriedades do material
MP,EX,1,ElasticModulus
MP,PRXY,1,Poisson
MP,DENS,1,ConcDens

! Keypoints
K,1,0,0,0
K,2,0,0,L2
K,3,L1,0,L2
K,4,L1,0,0

! Lines
L,1,2
L,2,3
L,3,4
L,4,1

! Area
AL,1,2,3,4

! Apoios
DL,1,,ALL,0
DL,2,,UX,0
DL,2,,UY,0
DL,2,,UZ,0
DL,3,,UX,0
DL,3,,UY,0
DL,3,,UZ,0
DL,4,,UX,0
DL,4,,UY,0
DL,4,,UZ,0

! Cargas
SFA,ALL,2,PRES,Load
ACEL,,Gravity

! Exibição
/view,,1,1,1
/replot

! Criar arquivos de resumo
*cfopen,'MAX-MX','TXT',,
  *vwrite,
  ('Momento MX máximo')
  *vwrite,
  (' ')
*cfclos
*cfopen,'MIN-MX','TXT',,
  *vwrite,
  ('Momento MX mínimo')
  *vwrite,
  (' ')
*cfclos
*cfopen,'MAX-MY','TXT',,
  *vwrite,
  ('Momento MY máximo')
  *vwrite,
  (' ')
*cfclos
*cfopen,'MIN-MY','TXT',,
  *vwrite,
  ('Momento MY mínimo')
  *vwrite,
  (' ')
*cfclos
*cfopen,'MAX-UY','TXT',,
  *vwrite,
  ('Deslocamento UY máximo')
  *vwrite,
  (' ')
*cfclos
*cfopen,'Resumo','TXT',,
  *vwrite,
  ('Resumo Geral')
  *vwrite,
  (' ')
  *vwrite,
  ('#; ESIZE; ECOUNT; MAX MX; MIN MX; MAX MY; MIN MY; MAX UY')
*cfclos

! Vista isométrica
APLOT
/view,1,1,-1,1
/auto,ALL
/replot

! Desativar exibição automática
/nopr
IMMED,0
/uis,REPLOT,0

PrevEcount=0

!! INÍCIO DO LOOP DE ESTUDO DE MALHA                           !!
*do,i,1,ModelCount,1

   !! PRÉ-PROCESSO                                             !!
   /prep7

   ! Definir tamanho da malha
   ESIDE=MinEsize+EsizeStep*(i-1)
   ! Definir nome do modelo
   MODNAME='Ex3.B-Modelo-%i%'

   ! Atribuir título ao modelo
   /title,%MODNAME%

   ! Malha
   AATT,1,,1,,1
   AESIZE,ALL,ESIDE
   AMESH,ALL
   APLOT

   ! Obter o número total de elementos no modelo
   ALLSEL
   *get,ElementCount,ELEM,0,COUNT

   *if,ElementCount,NE,PrevEcount,THEN

      PrevEcount=ElementCount

      !! SOLUÇÃO                                               !!
      /solu
      SOLVE

      !! PÓS-PROCESSO                                          !!
      /post1

      ! Ler resultados
      SET,,1

      ! Criar tabelas
      ETABLE,DESLOCY,U,Y
      ETABLE,MOMXLOC,SMISC,4
      ETABLE,MOMYLOC,SMISC,5

      ! Extrair os valores máximo e mínimo do momento fletor em X
      KEY='MOMXLOC'
      *get,ETCOUNT,ETAB,0,NCOL,MAX
      *do,k,1,ETCOUNT,1
         *get,ETNAME,ETAB,k,LAB
         *if,ETNAME,EQ,KEY,THEN
            *get,ENUM,ETAB,0,NLENG,MAX
            *dim,BENMOMX,ARRAY,ENUM
            *do,j,1,ENUM,1
               *get,ETVALUE,ETAB,k,ELEM,j
               BENMOMX(j)=ETVALUE
            *enddo
         *endif
      *enddo

      *vscfun,MAXBMX,MAX,BENMOMX
      *vscfun,MINBMX,MIN,BENMOMX

      ! Extrair os valores máximo e mínimo do momento fletor em Y
      KEY='MOMYLOC'
      *get,ETCOUNT,ETAB,0,NCOL,MAX
      *do,k,1,ETCOUNT,1
         *get,ETNAME,ETAB,k,LAB
         *if,ETNAME,EQ,KEY,THEN
            *get,ENUM,ETAB,0,NLENG,MAX
            *dim,BENMOMY,ARRAY,ENUM
            *do,j,1,ENUM,1
               *get,ETVALUE,ETAB,k,ELEM,j
               BENMOMY(j)=ETVALUE
            *enddo
         *endif
      *enddo

      *vscfun,MAXBMY,MAX,BENMOMY
      *vscfun,MINBMY,MIN,BENMOMY

      ! Extrair o valor mínimo do deslocamento em Y (flecha máxima)
      KEY='DESLOCY'
      *get,ETCOUNT,ETAB,0,NCOL,MAX
      *do,k,1,ETCOUNT,1
         *get,ETNAME,ETAB,k,LAB
         *if,ETNAME,EQ,KEY,THEN
            *get,ENUM,ETAB,0,NLENG,MAX
            *dim,DESUY,ARRAY,ENUM
            *do,j,1,ENUM,1
               *get,ETVALUE,ETAB,k,ELEM,j
               DESUY(j)=ETVALUE
            *enddo
         *endif
      *enddo

      *vscfun,MAXUY,MIN,DESUY

      ! Criar o arquivo de registro de dados específico do modelo
      *cfopen,MODNAME,'txt',,
        *vwrite,i
        ('MODELO ',F4.1)
        *vwrite,
        ('____________________')
        *vwrite,ESIDE
        ('ESIZE = ',F8.3)
        *vwrite,ElementCount
        ('ECOUNT = ',F8.0,TL1,' ')
        *vwrite,' '
        (A2)
        *vwrite,'Max MX:',MAXBMX
        (A8,F10.4)
        *vwrite,'Min MX:',MINBMX
        (A8,F10.4)
        *vwrite,'Max MY:',MAXBMY
        (A8,F10.4)
        *vwrite,'Min MY:',MINBMY
        (A8,F10.4)
        *vwrite,'Max UY:',MAXUY
        (A8,E)
      *cfclos

      ! Escrever resultados do modelo nos arquivos gerais de resumo
      *cfopen,'MAX-MX','TXT',,APPEND
        *vwrite,ElementCount,MAXBMX
        (F8.0,TL1,'; ',F10.4)
      *cfclos
      *cfopen,'MIN-MX','TXT',,APPEND
        *vwrite,ElementCount,MINBMX
        (F8.0,TL1,'; ',F10.4)
      *cfclos
      *cfopen,'MAX-MY','TXT',,APPEND
        *vwrite,ElementCount,MAXBMY
        (F8.0,TL1,'; ',F10.4)
      *cfclos
      *cfopen,'MIN-MY','TXT',,APPEND
        *vwrite,ElementCount,MINBMY
        (F8.0,TL1,'; ',F10.4)
      *cfclos
      *cfopen,'MAX-UY','TXT',,APPEND
        *vwrite,ElementCount,MAXUY
        (F8.0,TL1,'; ',E11.4)
      *cfclos
      *cfopen,'Resumo','TXT',,APPEND
        *vwrite,i,ESIDE,ElementCount,MAXBMX,MINBMX,MAXBMY,MINBMY,MAXUY
        (F5.0,TL1,'; ',F8.5,'; ',F8.0,TL1,'; ',E11.4,'; ',E11.4,'; ',E11.4,'; ',E11.4,'; ',E11.4)
      *cfclos

      *del,DESUY
      *del,BENMOMY
      *del,BENMOMX

      ! SAVE,'D:\Ansys\02 Curso\Modelos\03\B\Modelo\%MODNAME%','db'
      FINISH

   *endif

   !! PRÉ-PROCESSO                                             !!
   /prep7
   ACLEAR,ALL

   FINISH

*enddo

FINISH
