! Pré-processo
/PREP7

! Elemento BEAM 188
ET,1,188
KEYOPT,1,3,2

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3

! KEYPOINTS
! Barra
K,1,0,0,0
K,2,0,18,0
! Auxiliares
K,3,-1,0,0

! Lines
L,1,2

! PERFIS
! Perfil 1: W 310 x 23,8
SECTYPE,1,BEAM,I,W310X23,1
SECDATA,0.101,0.101,0.305,0.0067,0.0067,0.0056

! Definir atributos das linhas
! Pilar esquerdo
LSEL,S,LINE,,1
LATT,1,,,,3,,1
LSEL,ALL

! Apoios
DK,1,ALL
DK,2,UX,0,,,UZ,ROTX,ROTY,ROTZ

! Criar malha
LESIZE,ALL,0.1
LMESH,ALL

! CARREGAMENTO
! Pilar
FK,2,FY,-1000

! Exibição
/ESHAPE,1
EPLOT
/REPLOT

! Solucionar
/SOLU
ANTYPE,0
PSTRES,ON
SOLVE

! Pós-processo
/POST1

! Ler resultados
SET,,1

! Plotar deformada
PLDISP

! Tensão de Von Mises
PLNSOL,S,EQV

! Nova análise
/SOLU
ANTYPE,BUCKLE
BUCOPT,LANB,10,0,0,RANGE
MXPAND,10,0,0,1,0.001
SOLVE

! Pós-processo
/POST1

! Ler resultados
SET,LIST

! Ler resultados
SET,,1

! Múltiplas janelas
/window,ALL,OFF
/window,1,LEFT
/window,2,RIGHT
/view,1,,,1
/angle,1
/view,2,1
/angle,2

! Plotar deformada
PLDISP

! Capturar imagens dos modos de flambagem
*do,nthMode,1,10,1
   SET,,nthMode
   PLDISP
   modeNumString=CHRVAL(nthMode)
   fName=STRCAT('MODO',modeNumString)
   TAKEPIC,fName,800
*enddo

