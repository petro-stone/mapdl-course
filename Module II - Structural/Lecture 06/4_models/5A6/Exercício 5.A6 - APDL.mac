! Pré-processo
/PREP7

! Elemento BEAM 188
ET,1,188
KEYOPT,1,3,2

! Propriedades do material
MP,EX,1,2E11
MP,PRXY,1,0.3

! KEYPOINTS
K,1,0,0,0
K,2,6,0,0
K,3,12,0,0
K,4,6,6,0

! LINES
LARC,1,4,2,6
LARC,3,4,2,6

! PERFIL
SECTYPE,1,BEAM,CTUBE,D60X2,1
SECDATA,0.02775,0.03,20

! Apoios
DK,1,UX,0,,,UY,UZ,ROTX,ROTY
DK,3,UX,0,,,UY,UZ,ROTX,ROTY

! Criar malha
LESIZE,ALL,0.1
LMESH,ALL

! CARREGAMENTO
! Pilar
FK,4,FY,-1000

! Exibição
/ESHAPE,1
EPLOT
/REPLOT

! Solucionar
/SOLU
ANTYPE,0
PSTRES,ON
SOLVE

! Pós-processo
/POST1

! Ler resultados
SET,,1

! Plotar deformada
PLDISP

! Tensão de Von Mises
PLNSOL,S,EQV

! Nova análise
/SOLU
ANTYPE,BUCKLE
BUCOPT,LANB,10,0,0,RANGE
MXPAND,10,0,0,1,0.001
SOLVE

! Pós-processo
/POST1

! Ler resultados
SET,LIST

! Ler resultados
SET,,1

! Múltiplas janelas
/window,ALL,OFF
/window,1,LEFT
/window,2,RIGHT
/view,1,,,1
/angle,1
/view,2,1
/angle,2

! Plotar deformada
PLDISP

! Capturar imagens dos modos de flambagem
*do,nthMode,1,10,1
   SET,,nthMode
   PLDISP
   modeNumString=CHRVAL(nthMode)
   fName=STRCAT('MODO',modeNumString)
   TAKEPIC,fName,800
*enddo

