*del,i,,nopr
/output,outfile,out
/uis,msgpop,4

*cfopen,'results',txt
  *vwrite,
  (' ')
*cfclos

*do,i,1,3,1
  init
  /prep7

  ET,1,181

  MP,EX,1,200E9
  MP,PRXY,1,0.3

  /vup,,Z
  /view,1,0,1,0
  seltol,1e-8

  SECTYPE,1,SHELL,,ESPESS,3
  SECDATA,0.0127,1

  K,1,0,0,0
  K,2,0,0,5.9

  CYL4,0,0,0.11,,,,5.9

  CYLIND,0.11,,2.45,3.45,0,180

  VSBV,1,2

  VDELE,ALL

  csys,1 ! Nota: DSYS,1 para alterar EXIBIÇÃO para sistema de coordenadas cilíndricas
  asel,s,loc,x,0.11
  asel,inve
  csys,0

  ADELE,ALL

  ALLSEL

  AATT,1,,1,,1
  AESIZE,ALL,0.02
  AMESH,ALL

  ET,2,MASS21
  R,2,0

  TYPE,2
  REAL,2

  nsel,none
  kmesh,2
  *get,TopMasterNode,NODE,,NUM,MIN

  NSEL,S,LOC,Z,5.9
  *if,i,eq,1,then
    CERIG,TopMasterNode,ALL,all

  *elseif,i,eq,2,then
    RBE3,TopMasterNode,UXYZ,ALL

  *elseif,i,eq,3,then

    allsel
    r,3
    real,3

    et,3,targe170
    keyopt,3,2,0
    keyopt,3,4,111111

    et,4,conta175
    keyopt,4,2,2 ! Contact algorithm: MPC
    keyopt,4,4,0
    keyopt,4,12,5 ! Behavior: bonded (always)

    type,3
    tshap,pilo
    e,TopMasterNode

    lsel,s,loc,z,5.9
    nsll,s,1
    esln,s
    type,4
    esurf

  *endif

  allsel

  F,TopMasterNode,FZ,-500E3

  TYPE,2
  REAL,2

  nsel,none
  kmesh,1
  *get,BotMasterNode,node,,num,min

  nsel,s,loc,z,0
  *if,i,eq,1,then
    cerig,BotMasterNode,all,all
  *elseif,i,eq,2,then
    RBE3,BotMasterNode,ALL,ALL
  *elseif,i,eq,3,then

    allsel

    r,4
    real,4

    et,5,targe170
    keyopt,5,2,0
    keyopt,5,4,111111

    et,6,conta175
    keyopt,6,2,2 ! Contact algorithm: MPC
    keyopt,6,4,0
    keyopt,6,12,5 ! Behavior: bonded (always)

    type,5
    tshap,pilo
    e,BotMasterNode

    lsel,s,loc,z,0
    nsll,s,1
    esln,s
    type,6
    esurf

  *endif

  allsel
  d,BotMasterNode,ux,0,,,,uy,uz,rotz
  d,TopMasterNode,ux,0,,,,uy

  ALLSEL

  /solu
  SOLVE

  /post1
  SET,,1
  /view,1,1,0,0
  pldisp,2

  plnsol,s,eqv,2

  plnsol,u,y,2
  *get,uy_max,plnsol,,max
  *get,uy_min,plnsol,,min

  plnsol,u,x,2
  *get,ux_max,plnsol,,max
  *get,ux_min,plnsol,,min

  *cfopen,'results',txt,,append
    *vwrite,i
    ('Iteração: ',F5.0)
    *vwrite,uy_max
    (E20.6)
    *vwrite,uy_min
    (E20.6)
    *vwrite,ux_max
    (E20.6)
    *vwrite,ux_min
    (E20.6)
  *cfclos

  path,'long%i%',2,,100
  ppath,1,,0,-0.11,0
  ppath,2,,0,-0.11,5.9
  pdef,'UY',u,y
  pasave,all,'path_%i%',path

*enddo
